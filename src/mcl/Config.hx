package mcl;

import Io.ThreadedIo;
import Io.MultiThreadIo;
import Io.SyncIo;
import js.Syntax;
import js.lib.Object;
import haxe.crypto.Sha1;
import haxe.io.Path;

class EventDispatcher<T> {
	public function new() {
		this._subscribers = new Array<T->Void>();
	}

	@:keep
	public function subscribe(callback:T->Void) {
		this._subscribers.push(callback);
	}

	public function dispatch(event:T) {
		if (this._subscribers.length == 0)
			return;
		for (subscriber in this._subscribers) {
			subscriber(event);
		}
	}

	private var _subscribers:Array<T->Void>;
}

typedef PreBuildEvent = {};
typedef PostBuildEvent = {success:Bool};

class ConfigEvents {
	// private var _subscribers:Map<String, Array<Function>>;
	public function new() {}

	public final onPreBuild = new EventDispatcher<PreBuildEvent>();
	public final onPostBuild = new EventDispatcher<PostBuildEvent>();
}

private class ConfigUtil {
	public static var getPackId:Void->String = function() {
		var packId = Sha1.encode(Sys.getCwd());
		return packId;
	};

	public static function lock<T:{}>(obj:T):T {
		Object.freeze(obj);
		for (field in Reflect.fields(obj)) {
			var value = Reflect.field(obj, field);
			if (value != null && Syntax.typeof(value) == 'object') {
				lock(value);
			}
		}
		return obj;
	}
}

@:keep
@:keepSub
class Config {
	public var libDir:String = Path.join([Sys.programPath(), '..', '.mcblib']);
	public var events:ConfigEvents = new ConfigEvents();
	public var generatedDirName:String = 'zzz';

	/**
	 *
	 * The name of the scoreboard that will be used to store the internal state of the mcb.
	 */
	public var internalScoreboardName:String;

	/**
	 *
	 * The Io instance that will be used to handle file writing, this is not compatible with the `io-thread-count` argument
	 */
	public var io:Null<Io> = null;

	public var eqVarScoreboardName:String = 'mcb.eq.var';
	public var eqConstScoreboardName:String = 'mcb.eq.const';
	public var dontEmitComments:Bool = false;

	/**
	 * 
	 * The message to put at the top of every file that is generated by mcb that supports comments.
	 *
	 */
	public var header:String = "# Generated with MC-Build\n";

	// public var autoCreateScoreboard:Bool = true;
	private function new() {
		this.internalScoreboardName = 'mcb.internal';
	}

	public static function create(base:UserConfig):Config {
		ConfigUtil.lock(base); // make sure the user can't change the config after it's been created

		var c = new Config();
		if (base.libDir != null)
			c.libDir = base.libDir;
		if (base.generatedDirName != null)
			c.generatedDirName = base.generatedDirName;
		if (base.internalScoreboardName != null)
			c.internalScoreboardName = base.internalScoreboardName;
		if (base.ioThreadCount != null)
			switch (base.ioThreadCount) {
				case _ if (base.ioThreadCount < 1):
					throw 'io-thread-count must be greater than 0';
				case 1:
					c.io = new SyncIo();
				case 2:
					c.io = new ThreadedIo();
				default:
					c.io = new MultiThreadIo(base.ioThreadCount);
			}
		if (base.eqConstScoreboardName != null)
			c.eqConstScoreboardName = base.eqConstScoreboardName;
		if (base.eqVarScoreboardName != null)
			c.eqVarScoreboardName = base.eqVarScoreboardName;
		if (base.header != null)
			c.header = base.header;
		if (base.dontEmitComments != null)
			c.dontEmitComments = base.dontEmitComments;
		if (base.setup != null)
			base.setup(c);

		return c;
	}
}

@:keep
interface UserConfig {
	public var libDir:Null<String>;
	public var generatedDirName:Null<String>;

	/**
	 *
	 * The name of the scoreboard that will be used to store the internal state of the mcb.
	 */
	public var internalScoreboardName:Null<String>;

	public var eqVarScoreboardName:Null<String>;
	public var eqConstScoreboardName:Null<String>;

	/**
	 * 
	 * The message to put at the top of every file that is generated by mcb that supports comments.
	 *
	 */
	public var header:Null<String>;

	/**
	 * 
	 * The number of threads to use for file writing, this is not compatible with the `io` parameter or `io-thread-count` argument
	 * 
	 */
	public var ioThreadCount:Null<Int>;

	/**
	 * allows for binding of compiler events.
	 */
	public var setup:Null<Config->Void>;

	/**
	 * 
	 */
	public var dontEmitComments:Null<Bool>;
}
